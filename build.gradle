buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.12.0'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'pl.allegro.tech.build.axion-release'

sourceCompatibility = 1.8
targetCompatibility = 1.8

scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = 'v'
        versionSeparator = ''
        def origDeserialize = deserialize
        //append .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}
project.version = scmVersion.version

defaultTasks 'clean', 'build'

// produce a jar file for our source files
task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java
}

configurations {
    pluginCompile
    compile {
        extendsFrom pluginCompile
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'org.rundeck:rundeck-core:3.0.+'
    )

    pluginCompile(
            'com.google.cloud:libraries-bom:3.3.0',
            'com.google.http-client:google-http-client:1.36.0',
            'com.google.code.gson:gson:2.3.1',
            'com.google.http-client:google-http-client-jackson2:1.34.0',
            'org.slf4j:slf4j-log4j12:1.7.30'
    )

    testCompile(
            "org.codehaus.groovy:groovy-all:2.3.7",
            "org.spockframework:spock-core:0.7-groovy-2.0",
            "cglib:cglib-nodep:2.2.2"
    )
}

def pluginLibs = configurations.runtime.files.collect { "$it.name" }.join(" ")

jar {
    manifest {
        attributes 'Rundeck-Plugin-Name': 'DataDog Nodes Plugin',
                'Rundeck-Plugin-Description': 'Resource Model Source plugin that provides DataDog hosts as nodes.',
                'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.0+',
                'Rundeck-Plugin-Tags': 'java,datadog,resource model',
                'Rundeck-Plugin-Author': 'Scott Shields',
                'Rundeck-Plugin-Version': '1.2',
                'Rundeck-Plugin-License': 'Apache 2.0',
                'Rundeck-Plugin-Source-Link': 'https://github.com/wayfair-incubator/rundeck-datadog-nodes-plugin',
                'Rundeck-Plugin-Archive': 'true',
                'Rundeck-Plugin-Libs-Load-First': 'false',
                'Rundeck-Plugin-Classnames': 'com.wayfair.rundeck.plugin.resources.datadog.DatadogResourceModelSourceFactory',
                'Rundeck-Plugin-Libs': pluginLibs,
                'Rundeck-Plugin-File-Version': project.version
    }
    from configurations.pluginCompile
}

wrapper {
    gradleVersion = '4.7'
}